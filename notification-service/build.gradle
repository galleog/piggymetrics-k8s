apply plugin: 'org.springframework.boot'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.google.cloud.tools.jib'
apply plugin: 'nu.studer.jooq'
apply plugin: 'io.freefair.aspectj.post-compile-weaving'

description = 'notification-service'

dependencies {
    compile(
            project(':pgm-core'),
            project(':grpc-common'),
            'org.springframework.boot:spring-boot-starter-jooq',
            'org.springframework.cloud:spring-cloud-starter-stream-kafka',
            'org.springframework.cloud:spring-cloud-starter-kubernetes-config',
            'org.springframework.boot:spring-boot-starter-mail',
            'io.grpc:grpc-protobuf',
            'io.grpc:grpc-stub',
            'io.grpc:grpc-services',
            'com.salesforce.servicelibs:reactor-grpc-stub',
            'net.devh:grpc-server-spring-boot-starter',
            'net.devh:grpc-client-spring-boot-starter',
            'io.disc99:protocol-buffers-spring-cloud-stream-starter',
            'org.apache.commons:commons-lang3',
            'org.liquibase:liquibase-core',
            'com.google.guava:guava',
            'net.javacrumbs.shedlock:shedlock-spring',
            'net.javacrumbs.shedlock:shedlock-provider-jdbc-template',
            'org.aspectj:aspectjrt',
            'org.aspectj:aspectjweaver',
            'org.slf4j:jcl-over-slf4j',
            'org.postgresql:postgresql'
    )
    compile('commons-validator:commons-validator') {
        exclude module: 'commons-logging'
    }

    compileOnly(
            'org.projectlombok:lombok'
    )

    annotationProcessor(
            'org.projectlombok:lombok'
    )

    testCompile(
            'org.springframework.boot:spring-boot-starter-test',
            'org.springframework.cloud:spring-cloud-stream-test-support',
            'io.projectreactor:reactor-test',
            'io.grpc:grpc-testing',
            'org.testcontainers:postgresql',
            'org.testcontainers:junit-jupiter',
            'com.ninja-squad:DbSetup',
            'org.assertj:assertj-db',
            'org.mockito:mockito-junit-jupiter',
            'org.junit.jupiter:junit-jupiter-api',
            'commons-io:commons-io',
            'org.simplejavamail:simple-java-mail'
    )

    testCompileOnly(
            'org.projectlombok:lombok'
    )

    testAnnotationProcessor(
            'org.projectlombok:lombok'
    )

    testRuntimeOnly(
            'org.junit.jupiter:junit-jupiter-engine'
    )

    aspect(
            "org.springframework:spring-aspects"
    )

    jooqRuntime(
            project(':liquibase-tc'),
            'org.testcontainers:postgresql',
            'org.postgresql:postgresql',
            'org.slf4j:slf4j-nop'
    )
}

aspectj {
    version = "${ver.aspectj}"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${ver.protobuf}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${ver.grpc}"
        }
        reactor {
            artifact = "com.salesforce.servicelibs:reactor-grpc:${ver.reactiveGrpc}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                option 'enable_deprecated=false'
            }
            reactor {}
        }
    }
}

idea {
    module {
        sourceDirs += file("$buildDir/generated/source/proto/main/java")
        sourceDirs += file("$buildDir/generated/source/proto/main/grpc")
        sourceDirs += file("$buildDir/generated/source/proto/main/reactor")
        generatedSourceDirs += file("$buildDir/generated/source/proto/main/java")
        generatedSourceDirs += file("$buildDir/generated/source/proto/main/grpc")
        generatedSourceDirs += file("$buildDir/generated/source/proto/main/reactor")
    }
}

jib {
    from {
        image = 'galleog/grpc-health-probe:alpine'
    }
}

jooq {
    domain(sourceSets.main) {
        jdbc {
            driver = database.driver
            url = database.url
            user = database.username
            password = database.password
        }

        generator {
            database {
                name = 'org.jooq.meta.postgres.PostgresDatabase'
                includes = '.*'
                excludes = 'databasechangelog.* | shedlock.*'
                inputSchema = 'public'
                forcedTypes {
                    forcedType {
                        userType = 'com.github.galleog.piggymetrics.notification.domain.NotificationType'
                        enumConverter = true
                        expression = '.*\\.NOTIFICATION_TYPE'
                    }
                }
            }
            generate {
                deprecated = false
                javaTimeTypes = true
                indexes = false
            }
            target {
                packageName = 'com.github.galleog.piggymetrics.notification.domain'
                directory = "$buildDir/generated/source/jooq"
            }
        }
    }
}

generateDomainJooqSchemaSource.with {
    javaExecSpec = { JavaExecSpec s ->
        s.systemProperties << [changeLogFile: "${project.projectDir}/src/main/resources/db/changelog/db.changelog-master.yaml"]
    }
}
