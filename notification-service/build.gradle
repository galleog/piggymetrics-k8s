apply plugin: 'org.springframework.boot'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.google.cloud.tools.jib'
apply plugin: 'nu.studer.jooq'

description = 'notification-service'

dependencies {
    implementation(
            project(':pgm-core'),
            project(':grpc-common'),
            project(':r2dbc-jooq'),
            project(':protobuf-spring-cloud-stream'),
            'org.springframework.cloud:spring-cloud-starter-stream-kafka',
            'org.springframework.cloud:spring-cloud-starter-kubernetes-fabric8-config',
            'org.springframework.boot:spring-boot-starter-mail',
            'io.grpc:grpc-protobuf',
            'io.grpc:grpc-stub',
            'io.grpc:grpc-services',
            'com.salesforce.servicelibs:reactor-grpc-stub',
            'com.google.protobuf:protobuf-java-util',
            'net.devh:grpc-spring-boot-starter',
            'org.apache.commons:commons-lang3',
            'com.google.guava:guava',
            'net.javacrumbs.shedlock:shedlock-spring',
            'net.javacrumbs.shedlock:shedlock-provider-r2dbc'
    )
    implementation('commons-validator:commons-validator') {
        exclude module: 'commons-logging'
    }

    runtimeOnly(
            'org.slf4j:jul-to-slf4j',
            'org.postgresql:r2dbc-postgresql',
            'io.r2dbc:r2dbc-pool'
    )

    testImplementation(
            'org.springframework.boot:spring-boot-starter-test',
            'org.springframework:spring-jdbc',
            'io.projectreactor:reactor-test',
            'com.ninja-squad:DbSetup',
            'org.assertj:assertj-db',
            'org.mockito:mockito-junit-jupiter',
            'org.junit.jupiter:junit-jupiter',
            'commons-io:commons-io',
            'org.simplejavamail:simple-java-mail',
            'com.asarkar.grpc:grpc-test',
            'org.testcontainers:postgresql',
            'org.testcontainers:junit-jupiter'
    )
    testImplementation("org.springframework.cloud:spring-cloud-stream") {
        artifact {
            name = "spring-cloud-stream"
            extension = "jar"
            type ="test-jar"
            classifier = "test-binder"
        }
    }

    testRuntimeOnly(
            'org.postgresql:postgresql',
            'org.liquibase:liquibase-core'
    )

    jooqGenerator(
            project(':liquibase-tc'),
            'org.testcontainers:postgresql',
            'org.postgresql:postgresql'
    )
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${ver.protobuf}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${ver.grpc}"
        }
        reactor {
            artifact = "com.salesforce.servicelibs:reactor-grpc:${ver.reactiveGrpc}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                option 'enable_deprecated=false'
            }
            reactor {}
        }
    }
}

idea {
    module {
        sourceDirs += file("$buildDir/generated/source/proto/main/java")
        sourceDirs += file("$buildDir/generated/source/proto/main/grpc")
        sourceDirs += file("$buildDir/generated/source/proto/main/reactor")
        generatedSourceDirs += file("$buildDir/generated/source/proto/main/java")
        generatedSourceDirs += file("$buildDir/generated/source/proto/main/grpc")
        generatedSourceDirs += file("$buildDir/generated/source/proto/main/reactor")
    }
}

jib {
    from {
        image = grpcHealthProbeImage
    }
}

jooq {
    version = ver.jooq

    configurations {
        main {
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN

                jdbc {
                    driver = database.driver
                    url = database.url
                    user = database.username
                    password = database.password
                }

                generator {
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        includes = '.*'
                        excludes = 'databasechangelog.* | shedlock.*'
                        inputSchema = 'public'
                        forcedTypes {
                            forcedType {
                                userType = 'com.github.galleog.piggymetrics.notification.domain.NotificationType'
                                enumConverter = true
                                expression = '.*\\.NOTIFICATION_TYPE'
                            }
                        }
                    }
                    generate {
                        deprecated = false
                        javaTimeTypes = true
                        indexes = false
                    }
                    target {
                        packageName = 'com.github.galleog.piggymetrics.notification.domain'
                        directory = "$buildDir/generated/source/jooq"
                    }
                }
            }
        }
    }
}

tasks.named('generateJooq').configure {
    javaExecSpec = { JavaExecSpec s ->
        s.systemProperties << [changeLogFile: "${project.projectDir}/src/main/resources/db/changelog/db.changelog-master.yaml"]
    }
}
