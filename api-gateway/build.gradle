apply plugin: 'org.springframework.boot'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.google.cloud.tools.jib'

description = 'api-gateway'

dependencies {
    implementation(
            project(':pgm-core'),
            project(':grpc-common'),
            'org.springframework.boot:spring-boot-starter-webflux',
            'org.springframework.boot:spring-boot-starter-security',
            'org.springframework.boot:spring-boot-starter-actuator',
            'org.springframework.boot:spring-boot-autoconfigure',
            'org.springframework.cloud:spring-cloud-starter-kubernetes-fabric8-config',
            'org.springframework.security:spring-security-oauth2-resource-server',
            'org.springframework.security:spring-security-oauth2-jose',
            'io.projectreactor:reactor-core',
            'io.grpc:grpc-protobuf',
            'io.grpc:grpc-stub',
            'com.salesforce.servicelibs:reactor-grpc-stub',
            'net.devh:grpc-client-spring-boot-starter',
            'org.apache.commons:commons-lang3',
            'org.javamoney:moneta',
            'org.zalando:jackson-datatype-money',
            'com.google.guava:guava'
    )

    runtimeOnly(
            'org.slf4j:jul-to-slf4j'
    )

    testImplementation(
            'org.springframework.boot:spring-boot-starter-test',
            'org.springframework.security:spring-security-test',
            'io.projectreactor:reactor-test',
            'com.google.protobuf:protobuf-java-util',
            'com.asarkar.grpc:grpc-test',
            'org.junit.jupiter:junit-jupiter'
    )
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${ver.protobuf}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${ver.grpc}"
        }
        reactor {
            artifact = "com.salesforce.servicelibs:reactor-grpc:${ver.reactiveGrpc}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                option 'enable_deprecated=false'
            }
            reactor {}
        }
    }
}

idea {
    module {
        sourceDirs += file("$buildDir/generated/source/proto/main/java")
        sourceDirs += file("$buildDir/generated/source/proto/main/grpc")
        sourceDirs += file("$buildDir/generated/source/proto/main/reactor")
        generatedSourceDirs += file("$buildDir/generated/source/proto/main/java")
        generatedSourceDirs += file("$buildDir/generated/source/proto/main/grpc")
        generatedSourceDirs += file("$buildDir/generated/source/proto/main/reactor")
    }
}

jib {
    from {
        image = 'openjdk:11-jre-slim'
    }
}
